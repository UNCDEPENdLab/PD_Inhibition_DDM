TITLE:
Antagonismesque factor and flanker
DATA:
FILE = "flanker_traits_mplus_d687104fdcc17051863b941980a5fc36.dat";
 
VARIABLE:
NAMES = id T_Mistrust T_Manipulativeness T_Aggression T_SelfHarm correct rt cond
     block trial trial_z rt_inv run_trial run_trial_z prev_rt; 
 MISSING=.;
 
    WITHIN = cond block cond_block prev_rt trial_z run_trial_z;
    BETWEEN = T_Mistrust T_Manipulativeness T_Aggression T_SelfHarm;
    USEVARIABLES = id correct rt_inv cond block prev_rt trial_z run_trial_z 
      T_Mistrust T_Manipulativeness T_Aggression T_SelfHarm cond_block;
    CLUSTER = id;
    CATEGORICAL = correct;
  
DEFINE:

    cond = cond - 1; ! 0=congruent, 1=incongruent
    block = block - 1; ! 0=most_incon, 1=most_con
    cond_block = cond*block; ! 1=most_con, incongruent
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(15000);
    BCONVERGENCE=.02;
    CHAINS=2;
    PROCESSORS=4;
  
MODEL:

    %WITHIN%
    rt_inv ON block cond_block;
    condslo | rt_inv ON cond;
    runslo | rt_inv ON run_trial_z;
    trialslo | rt_inv ON trial_z;
    prevslo | rt_inv ON prev_rt;
    
    !treat these as fixed only (keep the model sane)
    correct ON trial_z run_trial_z block cond_block;
    b_condslo | correct ON cond;
    b_prevslo | correct ON prev_rt;
    
    correct WITH rt_inv; !allow RT-accuracy correlation
    
    %BETWEEN%
    
    !means of random slopes
    [condslo];
    [runslo];
    [trialslo];
    [prevslo];
    
    !variances of random slopes
    condslo;
    runslo;
    trialslo;
    prevslo;
    
    !slope correlations (removed because all n.s.)
    !condslo runslo trialslo prevslo WITH
    !   condslo runslo trialslo prevslo;
    
    [rt_inv]; !mean average inverse RT
    
    ! trait model
    antag BY T_Mistrust T_Manipulativeness T_Aggression T_SelfHarm;

    ! trait moderates flanker performance -- incongruency effect and stickiness of RT
    condslo ON antag;
    prevslo ON antag;
    
    !N.B. Leaving out the association of antag with rt_inv omits a hugely important relationship
    !  on the between level and will lead to bizarre problems -- like factor loadings near zero
    
    !allow antag as a predictor of average RT
    rt_inv ON antag;
    
    ! let factor correlate with other variables on the between level
    ! omitting this for now because it leads to estimation problems at times and when added individually, these are highly n.s.
    !antag WITH runslo;
    !antag WITH trialslo;
    
    [correct$1]; !person average
    b_condslo ON antag; !whether antagonism moderates the incongruency -> accuracy relationship
    
    b_prevslo WITH antag; !drives the model off the rails if we attempt b_prevslo ON antag
    
    !allow antag as predictor of overall accuracy
    correct on antag;
    
    !relationship between average RT and accuracy
    correct WITH rt_inv;
    
    !associations between continuous and binary random slopes
    !condslo WITH b_condslo;
    !prevslo WITH b_prevslo;
    
  
OUTPUT:
TECH1 TECH8 STANDARDIZED CINTERVAL;
PLOT:
TYPE = PLOT2;
