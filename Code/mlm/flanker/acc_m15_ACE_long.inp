TITLE:
Combined 4-factor MSEM Flanker ACC
DATA:
FILE = "acc_m15_ACE.dat";
 
VARIABLE:
NAMES = id correct rt cond block prev_rt prev_error prev_rt_z EXHIB ENTITL DETACH
     MPS_spR MPS_scR MPS_agR AGG MISTRUST MPS_alR MANIP PROPER MPS_clR IMPUL MPS_tdR
     MPS_acR HDWK; 
 MISSING=.;
 
    WITHIN = cond block prev_rt_z prev_error;
    USEVARIABLES = id correct cond block prev_rt_z
      prev_error EXHIB ENTITL DETACH MPS_spR MPS_scR
      MANIP p1 p2 p3 p4 p5;
    BETWEEN = EXHIB ENTITL DETACH MPS_spR MPS_scR MANIP p1 p2 p3 p4 p5;
    CLUSTER = id;
    CATEGORICAL = correct;
  
DEFINE:

    p1=MEAN(MISTRUST MPS_alR);
    p2=MEAN(PROPER MPS_tdR);
    p3=MEAN(MPS_clR IMPUL);
    p4=MEAN(MPS_acR HDWK);
    p5=MEAN(MPS_agR AGG);
    cond = cond - 1; ! 0=congruent, 1=incongruent
    block = block - 1; ! 0=most_incon, 1=most_con
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=200000 (120000);
    CHAINS=4;
    PROCESSORS=4;
  
MODEL:


%WITHIN%
    correct ON block;
    condslo | correct ON cond;        !random slope of cond
    prevslo | correct ON prev_rt_z;   !random slope of rt autocorrelation
    errslo | correct ON prev_error;   !random slope of previous error

    %BETWEEN%
    !means of random slopes
    [condslo];
    [prevslo];
    [errslo];

    !variances of random slopes
    condslo;
    prevslo;
    errslo;

    !slope correlations
    condslo prevslo errslo WITH
       condslo prevslo errslo;

    [correct$1]; !intercept: average correct

    !TRAIT MODEL
    agentic BY MPS_spR* EXHIB ENTITL;
    agentic@1;

    affiliat BY MPS_scR*1 DETACH;
    affiliat@1;

    antag BY p5* ! MPS_agR AGG
      p1        ! MISTRUST MPS_alR
      MANIP;
    antag@1;

    constraint BY
      p2*  !PROPER MPS_tdR
      p3  !MPS_clR IMPUL
      p4; !MPS_acR HDWK;
    constraint@1;

    antag constraint agentic affiliat WITH 
      antag constraint agentic affiliat;

    !identified in between-persons combined CFA
    AFFILIAT BY P1; !sig modification index
    constraint BY ENTITL; !sig modification index

    !TRAIT MODERATION

    condslo ON antag constraint agentic affiliat;
    prevslo ON antag constraint agentic affiliat;
    errslo ON antag constraint agentic affiliat;

    !allow proportion correct to be predicted by traits
    correct ON antag constraint agentic affiliat;
  
OUTPUT:

    TECH1 TECH8 CINTERVAL STANDARDIZED;
  
PLOT:
TYPE = PLOT2;
