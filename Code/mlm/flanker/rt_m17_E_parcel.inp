TITLE:
Extraversion and Detachment MSEM Flanker RT Parcel
DATA:
FILE = "rt_m17_E_parcel.dat";
 
VARIABLE:
NAMES = id rt cond block trial trial_z rt_inv run_trial run_trial_z prev_rt
     prev_error prev_rt_z POSTEMP EXHIB ENTITL DETACH MPS_wbR MPS_spR MPS_scR; 
 MISSING=.;
 
    WITHIN = cond block cond_block prev_rt_z prev_error trial_z run_trial_z;
    USEVARIABLES = id rt_inv cond block prev_rt_z 
      prev_error trial_z run_trial_z
      ENTITL DETACH MPS_scR p1 p2 cond_block;
    BETWEEN = ENTITL DETACH MPS_scR p1 p2;
    CLUSTER = id;
  
DEFINE:

    p1 = MEAN(MPS_spR EXHIB);
    p2 = MEAN(POSTEMP MPS_wbR);
    cond = cond - 1; ! 0=congruent, 1=incongruent
    block = block - 1; ! 0=most_incon, 1=most_con
    cond_block = cond*block; ! 1=most_con, incongruent
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(20000);
    CHAINS=4;
    PROCESSORS=4;
  
MODEL:

  
  %WITHIN%
    rt_inv ON block cond_block;
    condslo | rt_inv ON cond;        !random slope of cond
    prevslo | rt_inv ON prev_rt_z;   !random slope of rt autocorrelation
    errslo | rt_inv ON prev_error;   !random slope of previous error
    runslo | rt_inv ON run_trial_z;  !random slope of run_trial
    trialslo | rt_inv ON trial_z;    !random slope of trial

  %BETWEEN%
    !means of random slopes
    [condslo];
    [runslo];
    [trialslo];
    [prevslo];
    [errslo];
    
    !variances of random slopes
    condslo;
    runslo;
    trialslo;
    prevslo;
    errslo;
    
    !slope correlations
    condslo runslo trialslo prevslo errslo WITH
       condslo runslo trialslo prevslo errslo;
    
    [rt_inv]; !mean average inverse RT
    
    !TRAIT MODEL
    
    agentic BY p1*1 !MPS_spR EXHIB
      p2 !POSTEMP MPS_wbR
      ENTITL;
      
    agentic@1;
  
    affiliat BY MPS_scR*1 DETACH;
    affiliat@1;
    
    agentic WITH affiliat;
    
    !TRAIT MODERATION
    
    ! trait moderates flanker performance -- incongruency effect and stickiness of RT
    condslo ON agentic affiliat;
    prevslo ON agentic affiliat;
    errslo ON agentic affiliat;
    
    !N.B. Leaving out the association of antag with rt_inv 
    !omits a hugely important relationship.
    !Thus, allow antag as a predictor of average (person) RT
    rt_inv ON agentic affiliat;
  
OUTPUT:

    TECH1 TECH8 CINTERVAL STANDARDIZED;
  
PLOT:
TYPE = PLOT2;
