TITLE:
Equivalent model to lmer m17, drop run_trial
DATA:
FILE = "flanker_mplus.dat";
 
VARIABLE:
NAMES = id rt cond block trial trial_z rt_inv prev_rt prev_rt_z; 
 MISSING=.;
 
    WITHIN = cond block cond_block prev_rt_z prev_error trial_z;
    USEVARIABLES = id rt_inv cond block prev_rt_z 
      prev_error trial_z cond_block;
    CLUSTER = id;
  
DEFINE:

    cond = cond - 1; ! 0=congruent, 1=incongruent
    block = block - 1; ! 0=most_incon, 1=most_con
    cond_block = cond*block; ! 1=most_con, incongruent
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(10000);
    CHAINS=2;
    PROCESSORS=4;
  
MODEL:

    %WITHIN%
    rt_inv ON block cond_block;
    condslo | rt_inv ON cond;        !random slope of cond
    trialslo | rt_inv ON trial_z;    !random slope of trial
    prevslo | rt_inv ON prev_rt_z;   !random slope of rt autocorrelation
    errslo | rt_inv ON prev_error;   !random slope of previous error
    
    %BETWEEN%
    !means of random slopes
    [condslo];
    [trialslo];
    [prevslo];
    [errslo];
    
    !variances of random slopes
    condslo;
    trialslo;
    prevslo;
    errslo;
    
    !slope correlations
    condslo trialslo prevslo errslo WITH
       condslo trialslo prevslo errslo;
    
    [rt_inv]; !mean average inverse RT
  
OUTPUT:
TECH1 TECH8 STANDARDIZED CINTERVAL;
PLOT:
TYPE = PLOT2;
