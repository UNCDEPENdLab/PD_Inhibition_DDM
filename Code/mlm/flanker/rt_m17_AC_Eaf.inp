TITLE:
Antag, constraint, Affiliat MSEM Flanker RT
DATA:
FILE = "rt_m17_AC_Eaf.dat";
 
VARIABLE:
NAMES = id rt cond block trial trial_z rt_inv run_trial run_trial_z prev_rt
     prev_error prev_rt_z DETACH MPS_scR MPS_agR AGG MISTRUST MPS_alR MANIP PROPER
     MPS_clR IMPUL MPS_tdR MPS_acR HDWK; 
 MISSING=.;
 
    WITHIN = cond block cond_block prev_rt_z prev_error trial_z run_trial_z;
    USEVARIABLES = id rt_inv cond block prev_rt_z 
      prev_error trial_z run_trial_z
      DETACH MPS_scR 
      MANIP p1 p2 p3 p4 p5 cond_block;
    BETWEEN = DETACH MPS_scR MANIP p1 p2 p3 p4 p5;
    CLUSTER = id;
  
DEFINE:

    p1=MEAN(MISTRUST MPS_alR);
    p2=MEAN(PROPER MPS_tdR);
    p3=MEAN(MPS_clR IMPUL);
    p4=MEAN(MPS_acR HDWK);
    p5=MEAN(MPS_agR AGG);
    cond = cond - 1; ! 0=congruent, 1=incongruent
    block = block - 1; ! 0=most_incon, 1=most_con
    cond_block = cond*block; ! 1=most_con, incongruent
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(25000);
    CHAINS=4;
    PROCESSORS=4;
  
MODEL:

  
  %WITHIN%
    rt_inv ON block cond_block;
    condslo | rt_inv ON cond;        !random slope of cond
    prevslo | rt_inv ON prev_rt_z;   !random slope of rt autocorrelation
    errslo | rt_inv ON prev_error;   !random slope of previous error
    runslo | rt_inv ON run_trial_z;  !random slope of run_trial
    trialslo | rt_inv ON trial_z;    !random slope of trial
    
  %BETWEEN%
    !means of random slopes
    [condslo];
    [runslo];
    [trialslo];
    [prevslo];
    [errslo];
    
    !variances of random slopes
    condslo;
    runslo;
    trialslo;
    prevslo;
    errslo;
    
    !slope correlations
    condslo runslo trialslo prevslo errslo WITH
       condslo runslo trialslo prevslo errslo;
    
    [rt_inv]; !mean average inverse RT
    
    !TRAIT MODEL
    
    affiliat BY MPS_scR*1 DETACH;
    affiliat@1;
  
    antag BY p5* ! MPS_agR AGG
      p1        ! MISTRUST MPS_alR
      MANIP;
    antag@1;

    constraint BY
      p2*  !PROPER MPS_tdR
      p3  !MPS_clR IMPUL
      p4; !MPS_acR HDWK;
    constraint@1;

    antag constraint affiliat WITH antag constraint affiliat;

    !identified in between-persons combined CFA
    AFFILIAT BY P1; !sig modification index
    
    !TRAIT MODERATION
    
    ! trait moderates flanker performance -- incongruency effect and stickiness of RT
    condslo ON antag constraint affiliat;
    prevslo ON antag constraint affiliat;
    errslo ON antag constraint affiliat;
    
    rt_inv ON antag constraint affiliat;
  
OUTPUT:

    TECH1 TECH8 CINTERVAL STANDARDIZED;
  
PLOT:
TYPE = PLOT2;
