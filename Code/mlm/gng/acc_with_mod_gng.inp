TITLE:
Antagonism, Disinhibition GNG ACC
DATA:
FILE = "acc_with_mod_gng.dat";
 
VARIABLE:
NAMES = id response cond stim trial_z MPS_agR AGG MISTRUST MPS_alR MANIP PROPER
     MPS_clR IMPUL MPS_tdR MPS_acR HDWK; 
 MISSING=.;
 
    ! WITHIN = stim; !if my understanding is correct, this will only specify an effect at the within-level
    USEVARIABLES = id response stim cond trial_z 
      MANIP p1 p2 p3 p4 p5
      ts_ixn;
    BETWEEN = MANIP p1 p2 p3 p4 p5
      cond trial_z
      ts_ixn;
    
    CLUSTER = id;
  
DEFINE:

    ts_ixn = stim*trial_z; !ixn of overall trial and stimulus;
    p1=MEAN(MISTRUST MPS_alR);
    p2=MEAN(PROPER MPS_tdR);
    p3=MEAN(MPS_clR IMPUL);
    p4=MEAN(MPS_acR HDWK);
    p5=MEAN(MPS_agR AGG);
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(15000);
    BCONVERGENCE=.02;
    CHAINS=2;
    PROCESSORS=2;
  
MODEL:

    %WITHIN%
    
    b_stim | response ON stim; 
    
    
    %BETWEEN%
    [b_stim]; !slope means
    b_stim; !slope variances
    
        
    [response] (b0); !mean average ACC
    
    
    !TRAIT MODEL
        
    antag BY 
      p5* ! MPS_agR AGG
      p1        ! MISTRUST MPS_alR
      MANIP;
    antag@1;

    disinhib BY
      p2*  !PROPER MPS_tdR
      p3  !MPS_clR IMPUL
      p4 !MPS_acR HDWK;
      p1; 
    disinhib@1;

    antag WITH disinhib;
    
    !disinhib BY p1; !modification index cross-loading for fit. Included above.

    !TRAIT MODERATION
    
    ! trait moderates flanker performance
    b_stim ON antag (b_bA)
      disinhib (b_bD);
    
    stim ON antag (t_bA)
      disinhib (t_bD);
    
    
    ts_ixn ON antag (ixn_bA) 
      disinhib (ixn_bD);
    
    cond ON antag  
      disinhib; 
    
    !N.B. Leaving out the association of antag with rt_inv 
    !omits a hugely important relationship.
    !Thus, allow antag as a predictor of average (person) RT
    
    response ON antag (bA) 
      disinhib (bD);
    
  
OUTPUT:
TECH1 TECH8 STANDARDIZED CINTERVAL;
PLOT:
TYPE = PLOT2;
