TITLE:
Antagonism factor and gng
DATA:
FILE = "rt_mod_cfa_antagonism_a_mplus.dat";
 
VARIABLE:
NAMES = subj_idx rt trial_z trial rt_log_trim_grp prev_rt bltrz MANIP P5 P1; 
 MISSING=.;
 
    WITHIN = prev_rt trial_z bltrz trbltrz;
    BETWEEN = P1 P5 MANIP;
    USEVARIABLES = subj_idx rt_log_trim_grp bltrz prev_rt trial_z 
      P1 P5 MANIP trbltrz;
    CLUSTER = subj_idx;
  
DEFINE:

   trbltrz = bltrz*trial_z;
    
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(15000);
    BCONVERGENCE=.02;
    CHAINS=2;
    PROCESSORS=2;
  
MODEL:

    %WITHIN%
    !treat these as fixed only (keep the model sane)
    b_trbltrz | rt_log_trim_grp ON trbltrz; 
    b_prev_rt | rt_log_trim_grp ON prev_rt;
    b_trial_z | rt_log_trim_grp ON trial_z;
    b_bltrz | rt_log_trim_grp ON bltrz;
    
    %BETWEEN%
    [b_trbltrz b_prev_rt b_trial_z b_bltrz]; !slope means
    b_trbltrz b_prev_rt b_trial_z b_bltrz; !slope variances
    
    ! trait model
    antag BY P1 P5 MANIP;
    
    [rt_log_trim_grp]; !person average
    b_trbltrz ON antag; 
    b_prev_rt ON antag; !whether antagonism moderates the prev_rt -> accuracy relationship
    b_trial_z ON antag; !whether antagonism moderates the incongruency -> accuracy relationship
    b_bltrz ON antag; !whether antagonism moderates the prev_rt -> accuracy relationship
    
    
    !allow antag as predictor of overall accuracy
    !rt_log_trim_grp on antag;
    
    !correlation of random slopes with each other
    b_trbltrz WITH b_prev_rt;
    b_trbltrz WITH b_trial_z;
    b_trbltrz WITH b_bltrz;
    b_prev_rt WITH b_trial_z;
    b_prev_rt WITH b_bltrz;
    b_trial_z WITH b_bltrz;
    
  
OUTPUT:
TECH1 TECH8 STANDARDIZED CINTERVAL;
PLOT:
TYPE = PLOT2;
