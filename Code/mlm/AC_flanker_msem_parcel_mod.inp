TITLE:
Antagonism and Disinhibition BSEM CFA 3+3 Parcels, Flanker RT Mod
DATA:
FILE = "AC_flanker_msem_parcel_mod.dat";
 
VARIABLE:
NAMES = id rt cond block trial trial_z rt_inv run_trial run_trial_z prev_rt
     prev_error prev_rt_z MPS_agR AGG MISTRUST MPS_alR MANIP PROPER MPS_clR IMPUL
     MPS_tdR MPS_acR HDWK; 
 MISSING=.;
 
    WITHIN = cond block cond_block prev_rt_z prev_error trial_z run_trial_z;
    USEVARIABLES = id rt_inv cond block prev_rt_z 
      prev_error trial_z run_trial_z
      MANIP p1 p2 p3 p4 p5 cond_block;
    BETWEEN = MANIP p1 p2 p3 p4 p5;
    CLUSTER = id;
  
DEFINE:

    p1=MEAN(MISTRUST MPS_alR);
    p2=MEAN(PROPER MPS_tdR);
    p3=MEAN(MPS_clR IMPUL);
    p4=MEAN(MPS_acR HDWK);
    p5=MEAN(MPS_agR AGG);
    cond = cond - 1; ! 0=congruent, 1=incongruent
    block = block - 1; ! 0=most_incon, 1=most_con
    cond_block = cond*block; ! 1=most_con, incongruent
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(15000);
    CHAINS=2;
    PROCESSORS=4;
  
MODEL:

  
  %WITHIN%
    rt_inv ON block cond_block;
    condslo | rt_inv ON cond;        !random slope of cond
    prevslo | rt_inv ON prev_rt_z;   !random slope of rt autocorrelation
    errslo | rt_inv ON prev_error;   !random slope of previous error
    runslo | rt_inv ON run_trial_z;  !random slope of run_trial
    trialslo | rt_inv ON trial_z;    !random slope of trial

    
  %BETWEEN%
    !means of random slopes
    [condslo];
    [runslo];
    [trialslo];
    [prevslo];
    [errslo];
    
    !variances of random slopes
    condslo;
    runslo;
    trialslo;
    prevslo;
    errslo;
    
    !slope correlations
    condslo runslo trialslo prevslo errslo WITH
       condslo runslo trialslo prevslo errslo;
    
    [rt_inv]; !mean average inverse RT
    
    !TRAIT MODEL
        
    antag BY p5* ! MPS_agR AGG
      p1        ! MISTRUST MPS_alR
      MANIP;
    antag@1;

    inhib BY
      p2*  !PROPER MPS_tdR
      p3  !MPS_clR IMPUL
      p4; !MPS_acR HDWK;
    inhib@1;

    antag WITH inhib;
    
    INHIB BY P1; !modification index cross-loading for fit

    !TRAIT MODERATION
    
    ! trait moderates flanker performance -- incongruency effect and stickiness of RT
    condslo ON antag inhib;
    prevslo ON antag inhib;
    
    !errslo ON antag inhib; !highly non-significant
    
    !N.B. Leaving out the association of antag with rt_inv 
    !omits a hugely important relationship.
    !Thus, allow antag as a predictor of average (person) RT
    rt_inv ON antag inhib;
  
OUTPUT:

    TECH1 TECH8 STANDARDIZED CINTERVAL;
  
PLOT:
TYPE = PLOT2;
