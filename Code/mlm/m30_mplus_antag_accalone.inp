TITLE:
Antagonismesque factor and flanker
DATA:
FILE = "flanker_traits_mplus_2e7eafb6675a90ffa5c133f53b171e5d.dat";
 
VARIABLE:
NAMES = id T_Mistrust T_Manipulativeness T_Aggression T_SelfHarm correct rt cond
     block trial trial_z run_trial run_trial_z prev_rt; 
 MISSING=.;
 
    WITHIN = cond block cond_block prev_rt trial_z run_trial_z;
    BETWEEN = T_Mistrust T_Manipulativeness T_Aggression T_SelfHarm;
    USEVARIABLES = id correct cond block prev_rt trial_z run_trial_z 
      T_Mistrust T_Manipulativeness T_Aggression T_SelfHarm cond_block;
    CLUSTER = id;
    CATEGORICAL = correct;
  
DEFINE:

    cond = cond - 1; ! 0=congruent, 1=incongruent
    block = block - 1; ! 0=most_incon, 1=most_con
    cond_block = cond*block; ! 1=most_con, incongruent
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(15000);
    BCONVERGENCE=.02;
    CHAINS=2;
    PROCESSORS=4;
  
MODEL:

    %WITHIN%
    !treat these as fixed only (keep the model sane)
    correct ON trial_z run_trial_z block cond_block;
    b_condslo | correct ON cond;
    b_prevslo | correct ON prev_rt;
    
    %BETWEEN%
    [b_condslo b_prevslo]; !slope means
    b_condslo b_prevslo; !slope variances
    
    ! trait model
    antag BY T_Mistrust T_Manipulativeness T_Aggression T_SelfHarm;

    [correct$1]; !person average
    b_condslo ON antag; !whether antagonism moderates the incongruency -> accuracy relationship
    b_prevslo ON antag; !whether antagonism moderates the prev_rt -> accuracy relationship
    
    !allow antag as predictor of overall accuracy
    correct on antag;
    
    !correlation of random slopes with each other
    b_condslo WITH b_prevslo;
  
OUTPUT:
TECH1 TECH8 STANDARDIZED CINTERVAL;
PLOT:
TYPE = PLOT2;
