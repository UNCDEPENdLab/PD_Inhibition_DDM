TITLE:
Agentic and Affiliative Extraversion MSEM Flanker ACC
DATA:
FILE = "Dom_modela_flanker_acc_msem.dat";
 
VARIABLE:
NAMES = id correct rt cond block prev_rt prev_error prev_rt_z EXHIB ENTITL DETACH
     MPS_spR MPS_scR; 
 MISSING=.;
 
    WITHIN = cond block prev_rt_z prev_error;
    USEVARIABLES = id correct cond block prev_rt_z prev_error 
      EXHIB ENTITL DETACH MPS_spR MPS_scR;
    BETWEEN = EXHIB ENTITL DETACH MPS_spR MPS_scR;
    CLUSTER = id;
    CATEGORICAL = correct;
  
DEFINE:

    cond = cond - 1; ! 0=congruent, 1=incongruent
    block = block - 1; ! 0=most_incon, 1=most_con
  
ANALYSIS:

    TYPE=TWOLEVEL RANDOM;
    ESTIMATOR=BAYES;
    BITERATIONS=(20000);
    CHAINS=4;
    PROCESSORS=4;
  
MODEL:

    %WITHIN%
    correct ON block;
    condslo | correct ON cond;        !random slope of cond
    prevslo | correct ON prev_rt_z;   !random slope of rt autocorrelation
    errslo | correct ON prev_error;   !random slope of previous error
    
    %BETWEEN%
    !means of random slopes
    [condslo];
    [prevslo];
    [errslo];
    
    !variances of random slopes
    condslo;
    prevslo;
    errslo;
    
    !slope correlations
    condslo prevslo errslo WITH
       condslo prevslo errslo;
    
    [correct$1]; !intercept: average correct
    
    !TRAIT MODEL
    agentic BY MPS_spR* EXHIB ENTITL;
    agentic@1;
    
    affiliat BY MPS_scR*1 DETACH;
    affiliat@1;
    
    agentic WITH affiliat;

    !TRAIT MODERATION

    ! trait moderates flanker performance -- incongruency effect and stickiness of RT
    condslo ON agentic affiliat;
    prevslo ON agentic affiliat;
    errslo ON agentic affiliat;
    
    !allow proportion correct to be predicted by traits
    correct ON agentic affiliat;
  
OUTPUT:

    TECH1 TECH8 CINTERVAL STANDARDIZED;
  
PLOT:
TYPE = PLOT2;
